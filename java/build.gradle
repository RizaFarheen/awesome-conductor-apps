ext {
    versions = [revConductorClient          : '1.0.5',
                conductor                   : '3.9.30-orkes',
                revTestContainer            : '1.17.2',
                log4j                       : '2.17.1',
                revSpotifyCompletableFutures: '0.3.3',
                revLombok                   : '1.18.30',
                revJedis                    : '3.8.0',
                awaitility                  : '4.2.0',
                commonsLang                 : '3.12.0',
                jackson                     : '2.11.4!!',
                jersey                      : '1.19.4',
                junit                       : '5.9.0',
                slf4j                       : '1.7.36',
                spectator                   : '1.3.7',
                awsSsm                      : '1.12.300',
                azureSsm                    : '4.2.3',
                azureIdentity               : '1.3.7',
                eureka                      : '1.10.17',
                wiremock                    : '2.33.2',
                ioGRPC                      : '1.49.0',
                protoBuf                    : '3.19.4',
                lombok                      : '1.18.24',
                revJAXRS                    : '2.1.1',
                revJerseyCommon             : '2.22.2',
                netty                       : '4.1.77.Final',]
}

subprojects { subproject ->
    group = 'io.orkes.conductor'
    apply plugin: 'java'

    repositories {
        mavenCentral()
        mavenLocal()
        maven {
            url "https://s01.oss.sonatype.org/content/repositories/releases/"
        }
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
        testCompileOnly {
            extendsFrom annotationProcessor
        }
        all {
            exclude group: 'org.apache.logging.log4j', module: 'log4j-slf4j-impl'
            exclude group: 'org.slf4j', module: 'slf4j-log4j12'
            if (subproject.name != 'orkes-conductor-workers') {
                exclude group: 'com.netflix.conductor'
            }
        }
    }

    dependencies {
        implementation "org.apache.logging.log4j:log4j-core:${versions.log4j}!!"
        implementation "org.apache.logging.log4j:log4j-api:${versions.log4j}!!"
        implementation "org.apache.logging.log4j:log4j-slf4j-impl:${versions.log4j}!!"
        implementation "org.apache.logging.log4j:log4j-jul:${versions.log4j}!!"
        implementation "org.apache.logging.log4j:log4j-web:${versions.log4j}!!"
        implementation "org.apache.logging.log4j:log4j-to-slf4j:${versions.log4j}!!"

        compileOnly "org.projectlombok:lombok:${versions.revLombok}"
        annotationProcessor "org.projectlombok:lombok:${versions.revLombok}"
        testAnnotationProcessor "org.projectlombok:lombok:${versions.revLombok}"
    }

    test {
        testLogging {
            events = ["SKIPPED", "FAILED"]
            exceptionFormat = "full"
            showStandardStreams = true
        }
    }


    compileJava {
        sourceCompatibility = 11
        targetCompatibility = 11
    }
}